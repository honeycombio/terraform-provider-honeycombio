# Data Source: honeycombio_recipient

`honeycombio_recipient` data source provides details about a specific recipient in the Team.

The ID of an existing recipient can be used when adding recipients to triggers or burn alerts.

~> **Warning** Terraform will fail unless exactly one recipient is returned by the search. Ensure that your search is specific enough to return a single recipient ID only.
If you want to match multiple recipients, use the `honeycombio_recipients` data source instead.

## Example Usage

```terraform
# search for a Slack recipient with channel name "honeycomb-triggers"
data "honeycombio_recipient" "slack" {
  type    = "slack"

  detail_filter {
    name  = "channel"
    value = "#honeycomb-triggers"
  }
}

data "honeycombio_query_specification" "example" {
  calculation {
    op     = "AVG"
    column = "duration_ms"
  }
}

resource "honeycombio_trigger" "example" {
  name        = "Requests are slower than usual"

  query_json = data.honeycombio_query_specification.example.json
  dataset    = var.dataset

  threshold {
    op    = ">"
    value = 1000
  }

  recipient {
    type   = "email"
    target = "hello@example.com"
  }

  # add an already existing recipient
  recipient {
    id = data.honeycombio_recipient.slack.id
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `type` (String) The type of recipient.

### Optional

- `dataset` (String, Deprecated) Deprecated: recipients are now a Team-level construct. Any provided 'dataset' value will be ignored.
- `detail_filter` (Block List, Max: 1) Attributes to filter the recipients with. (see [below for nested schema](#nestedblock--detail_filter))
- `target` (String, Deprecated) Deprecated: use 'detail_filter' instead. Target of the trigger or burn alert, this has another meaning depending on the type of recipient.

### Read-Only

- `address` (String) The email recipient's address -- if of type `email`
- `channel` (String) The Slack recipient's channel -- if of type `slack`
- `id` (String) The ID of this resource.
- `integration_key` (String, Sensitive) The PagerDuty recipient's key -- if of type `pagerduty`
- `integration_name` (String) The PagerDuty recipient's name -- if of type `pagerduty`
- `name` (String) The webhook recipient's name -- if of type `webhook` or `msteams`
- `secret` (String, Sensitive) The webhook recipient's secret -- if of type `webhook`
- `url` (String) The webhook recipient's URL -- if of type `webhook` or `msteams`

<a id="nestedblock--detail_filter"></a>
### Nested Schema for `detail_filter`

Required:

- `name` (String) The name of the detail field to filter by

Optional:

- `value` (String) The value of the detail field to match on.
- `value_regex` (String) A regular expression string to apply to the value of the detail field to match on.

The `type` attribute determines which detail fields are available for filtering:

Type             | Available Fields
-----------------|-------------------------
email            | `address`
marker           | `name`
msteams          | `name`, `url`
msteams_workflow | `name`, `url`
pagerduty        | `integration_name`
slack            | `channel`
webhook          | `name`, `url`

~> **Note** One of `value` or `value_regex` is required for each `detail_filter` block.

# Resource: honeycombio_query

Creates a Query scoped to a Dataset or Environment.

Queries can be used by Triggers and Boards, or be executed via the [Query Data API](https://docs.honeycomb.io/api/query-results/).

-> Queries are immutable and can not be deleted -- only created or read.
  Any changes will result in a new query object being created.

## Example Usage

```terraform
variable "dataset" {
  type = string
}

resource "honeycombio_derived_column" "duration_ms_log10" {
  alias       = "duration_ms_log10"
  expression  = "LOG10($duration_ms)"
  description = "LOG10 of duration_ms"

  dataset = var.dataset
}

data "honeycombio_query_specification" "example" {
  calculation {
    op     = "P90"
    column = "duration_ms"
  }

  calculation {
    op     = "HEATMAP"
    column = $honeycombio_derived_column.duration_ms_log10.alias
  }

  filter {
    column = "duration_ms"
    op     = "exists"
  }

}

resource "honeycombio_query" "example" {
  dataset    = var.dataset
  query_json = data.honeycombio_query_specification.example.json

  lifecycle {
    replace_triggered_by = [
      // re-create the query if the derived column is changed
      // to ensure we're using the latest definition
      honeycombio_derived_column.duration_ms_log10
    ]
  }
}
```

-> **Note** If you are referencing a [Derived Column](derived_column.md) in your query and want to ensure you are always using the latest definition
  of the derived column you should use the [replace_triggered_by](https://developer.hashicorp.com/terraform/language/meta-arguments/lifecycle#replace_triggered_by)
  lifecycle argument as shown in the example above.

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `query_json` (String) A JSON object describing the query according to the Query Specification. While the JSON can be constructed manually, it is easiest to use the `honeycombio_query_specification` data source.

### Optional

- `dataset` (String) The dataset this Query is associated with. If not set, it will be Environment-wide.

### Read-Only

- `id` (String) The unique identifier for this Query.

## Import

Querys can be imported using a combination of the dataset name and their ID, e.g.

```
$ terraform import honeycombio_query.my_query my-dataset/bj8BwOa1uRz
```

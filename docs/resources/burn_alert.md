# Resource: honeycombio_burn_alert

Creates a burn alert.

For more information about burn alerts,
check out [Define Burn Alerts](https://docs.honeycomb.io/working-with-your-data/slos/burn-alerts).

## Example Usage

### Basic Example - Exhaustion Time Burn Alert

```terraform
variable "dataset" {
  type = string
}

variable "slo_id" {
  type = string
}

resource "honeycombio_burn_alert" "example_alert" {
  alert_type         = "exhaustion_time"
  exhaustion_minutes = 480
  description        = "Exhaustion burn alert description"

  dataset = var.dataset
  slo_id  = var.slo_id

  # one or more recipients
  recipient {
    type   = "email"
    target = "hello@example.com"
  }

  recipient {
    type   = "slack"
    target = "#example-channel"
  }
}
```

### Basic Example - Budget Rate Burn Alert

```terraform
variable "dataset" {
  type = string
}

variable "slo_id" {
  type = string
}

resource "honeycombio_burn_alert" "example_alert" {
  alert_type                   = "budget_rate"
  budget_rate_window_minutes   = 480
  budget_rate_decrease_percent = 1
  description                  =  "my example description"

  dataset = var.dataset
  slo_id  = var.slo_id

  # one or more recipients
  recipient {
    type   = "webhook"
    target = "name of the webhook"
  }
}
```

### Example - Exhaustion Time Burn Alert with PagerDuty Recipient and Severity

```terraform
variable "dataset" {
  type = string
}

variable "slo_id" {
  type = string
}

data "honeycombio_recipient" "pd_prod" {
  type = "pagerduty"

  detail_filter {
    name  = "integration_name"
    value = "Prod On-Call"
  }
}

resource "honeycombio_burn_alert" "example_alert" {
  exhaustion_minutes = 60
  description        = "Burn alert description"
  dataset            = var.dataset
  slo_id             = var.slo_id

  recipient {
    id = data.honeycombio_recipient.pd_prod.id

    notification_details {
      pagerduty_severity = "critical"
    }
  }
}
```

### Example - Exhaustion Time Burn Alert with Webhook Recipient and Notification Variable

```terraform
variable "dataset" {
  type = string
}

variable "slo_id" {
  type = string
}

data "honeycombio_recipient" "custom_webhook" {
    type = "webhook"

    detail_filter {
        name  = "name"
        value = "My Custom Webhook"
    }
}

resource "honeycombio_burn_alert" "example_alert" {
    exhaustion_minutes = 60
    description        = "Burn alert description"
    dataset            = var.dataset
    slo_id             = var.slo_id

    dataset = var.dataset
    slo_id  = var.slo_id

    recipient {
      id = data.honeycombio_recipient.custom_webhook.id

      notification_details {
          variable {
              name = "severity"
              value = "info"
          }
      }
    }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `slo_id` (String) The ID of the SLO that this Burn Alert is associated with.

### Optional

- `alert_type` (String) The alert type of this Burn Alert.
- `budget_rate_decrease_percent` (Number) The percent the budget has decreased over the budget rate window.
- `budget_rate_window_minutes` (Number) The time period, in minutes, over which a budget rate will be calculated.
- `dataset` (String) The dataset this Burn Alert is associated with. Will be deprecated in a future release.
- `description` (String) A description for this Burn Alert.
- `exhaustion_minutes` (Number) The amount of time, in minutes, remaining before the SLO's error budget will be exhausted and the alert will fire.
- `recipient` (Block Set) Zero or more recipients to notify when the resource fires. (see [below for nested schema](#nestedblock--recipient))

### Read-Only

- `id` (String) The unique identifier for this Burn Alert.

<a id="nestedblock--recipient"></a>
### Nested Schema for `recipient`

Optional:

- `id` (String) The ID of an existing recipient.
- `notification_details` (Block List) Additional details to send along with the notification. (see [below for nested schema](#nestedblock--recipient--notification_details))
- `target` (String) Target of the notification, this has another meaning depending on the type of recipient.
- `type` (String) The type of the notification recipient.

<a id="nestedblock--recipient--notification_details"></a>
### Nested Schema for `recipient.notification_details`

Optional:

- `pagerduty_severity` (String) The severity to set with the PagerDuty notification. If no severity is provided, 'critical' is assumed.
- `variable` (Block Set) The variables to set with the webhook notification. (see [below for nested schema](#nestedblock--recipient--notification_details--variable))

<a id="nestedblock--recipient--notification_details--variable"></a>
### Nested Schema for `recipient.notification_details.variable`

Required:

- `name` (String) The name of the variable

Optional:

- `value` (String) The value of the variable

Each `recipient` block can either refer to an existing recipient (already present in another burn alert or trigger) or a new recipient.
When specifying an existing recipient, only `id` may be set.
To retrieve the ID of an existing recipient, refer to the [`honeycombio_recipient`](../data-sources/recipient.md) data source.

| Type      | Target              |
| --------- | ------------------- |
| email     | an email address    |
| pagerduty | _N/A_               |
| slack     | name of the channel |
| webhook   | name of the webhook |

## Import

Burn alerts can be imported using by using their ID combined with their dataset.
If the burn alert is a multi-dataset (MD) burn alert, the dataset is not provided.

### Burn Alert

```
$ terraform import honeycombio_burn_alert.my_alert my-dataset/bj9BwOb1uKz
```

### Multi-dataset Burn Alert

```
$ terraform import honeycombio_burn_alert.my_alert bc9XwOb2yJu
```

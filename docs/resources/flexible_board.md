# Resource: honeycombio_flexible_board

Creates a flexible board. For more information about boards, check out [Create Custom Boards](https://docs.honeycomb.io/observe/boards).

## Example Usage

### Simple Flexible Board

```terraform
variable "dataset" {
  type = string
}

data "honeycombio_query_specification" "latency_by_userid" {
  time_range = 86400
  breakdowns = ["app.user_id"]

  calculation {
    op     = "HEATMAP"
    column = "duration_ms"
  }

  calculation {
    op     = "P99"
    column = "duration_ms"
  }

  filter {
    column = "trace.parent_id"
    op     = "does-not-exist"
  }

  order {
    column = "duration_ms"
    op     = "P99"
    order  = "descending"
  }

}

resource "honeycombio_query" "latency_by_userid" {
  dataset    = var.dataset
  query_json = data.honeycombio_query_specification.latency_by_userid.json
}

resource "honeycombio_query_annotation" "latency_by_userid" {
  dataset     = var.dataset
  query_id    = honeycombio_query.latency_by_userid.id
  name        = "Latency by User"
  description = "A breakdown of trace latency by User over the last 24 hours"
}

resource "honeycombio_derived_column" "request_latency_sli" {
  alias       = "sli.request_latency"
  description = "SLI: request latency less than 300ms"
  dataset     = var.dataset

  # heredoc also works
  expression = file("../sli/sli.request_latency.honeycomb")

  lifecycle {
    # in order to avoid potential conflicts with renaming the derived column
    # while in use by the SLO, we set create_before_destroy to true
    create_before_destroy = true
  }
}

resource "honeycombio_slo" "slo" {
  name              = "Latency SLO"
  description       = "example SLO"
  dataset           = var.dataset
  sli               = honeycombio_derived_column.request_latency_sli.alias
  target_percentage = 99.9
  time_period       = 30

  tags = {
    team = "web"
  }
}

resource "honeycombio_flexible_board" "overview" {
  name        = "Service Overview"
  description = "My flexible board description"

  tags = {
    team    = "web"
    project = "secret"
  }

  preset_filter {
    column = "app.service.name"
    alias = "Service name"
  }

  preset_filter {
    column = "app.handler.route"
    alias = "Route"
  }

  panel {
    type = "query"

    query_panel {
      query_id            = honeycombio_query.latency_by_userid.id
      query_annotation_id = honeycombio_query_annotation.latency_by_userid.id
      query_style         = "combo"
      visualization_settings {
        use_utc_xaxis = true
        chart {
          chart_type          = "line"
          chart_index         = 0
          omit_missing_values = true
          use_log_scale       = true
        }
      }
    }
  }

  panel {
    type = "slo"
    slo_panel {
      slo_id = honeycombio_slo.slo.id
    }
  }

  panel {
    type = "text"
    text_panel {
      content = <<EOF
# This is fancy text content

This text panel supports:
- **Markdown formatting**
- Multiple lines of content
- Rich text features

## Additional Section
More content can be added here with proper spacing.
EOF
    }
  }
}
```

<!-- schema generated by tfplugindocs -->

## Schema

### Required

- `name` (String) The name of the Board.

### Optional

- `description` (String) The description of the Board. Supports Markdown.
- `panel` (Block List) List of panels to render on the board. (see [below for nested schema](#nestedblock--panel))
- `preset_filter` (Block List) List of preset filters for the board. (see [below for nested schema](#nestedblock--preset_filter))
- `tags` (Map of String) A map of tags to assign to the resource.

### Read-Only

- `board_url` (String) The URL of the Board in the Honeycomb UI.
- `id` (String) The ID of the Board.

<a id="nestedblock--panel"></a>

### Nested Schema for `panel`

Required:

- `type` (String) The panel type, either "query", "slo", or "text".

Optional:

- `position` (Block, Optional) Manages the position of the panel on the board. (see [below for nested schema](#nestedblock--panel--position))
- `query_panel` (Block List) A query panel to be displayed on the Board. (see [below for nested schema](#nestedblock--panel--query_panel))
- `slo_panel` (Block List) A Service Level Objective(SLO) panel to be displayed on the Board. (see [below for nested schema](#nestedblock--panel--slo_panel))
- `text_panel` (Block List) A text panel to be displayed on the Board. (see [below for nested schema](#nestedblock--panel--text_panel))

<a id="nestedblock--panel--position"></a>

### Nested Schema for `panel.position`

Optional:

- `height` (Number) The height of the panel.
- `width` (Number) The width of the panel.
- `x_coordinate` (Number) The X coordinate of the panel.
- `y_coordinate` (Number) The Y coordinate of the panel.

<a id="nestedblock--panel--query_panel"></a>

### Nested Schema for `panel.query_panel`

Required:

- `query_annotation_id` (String) Query annotation ID.
- `query_id` (String) Query ID to be rendered in the panel.

Optional:

- `query_style` (String) The visual style of the query (e.g., 'graph', 'combo').
- `visualization_settings` (Block List) (see [below for nested schema](#nestedblock--panel--query_panel--visualization_settings))

<a id="nestedblock--panel--query_panel--visualization_settings"></a>

### Nested Schema for `panel.query_panel.visualization_settings`

Optional:

- `chart` (Block List) (see [below for nested schema](#nestedblock--panel--query_panel--visualization_settings--chart))
- `hide_compare` (Boolean) Hide comparison values.
- `hide_hovers` (Boolean) Disable Graph tooltips in the results display when hovering over a graph.
- `hide_markers` (Boolean) Hide markers from appearing on graph.
- `prefer_overlaid_charts` (Boolean) Combine any visualized AVG, MIN, MAX, and PERCENTILE clauses into a single chart.
- `use_utc_xaxis` (Boolean) Display UTC Time X-Axis or Localtime X-Axis.

<a id="nestedblock--panel--query_panel--visualization_settings--chart"></a>

### Nested Schema for `panel.query_panel.visualization_settings.chart`

Optional:

- `chart_index` (Number) Index of the chart this configuration controls.
- `chart_type` (String) Type of chart (e.g., 'line', 'bar').
- `omit_missing_values` (Boolean) Omit missing values from the visualization.
- `use_log_scale` (Boolean) Use logarithmic scale on Y axis.

<a id="nestedblock--panel--slo_panel"></a>

### Nested Schema for `panel.slo_panel`

Required:

- `slo_id` (String) SLO ID to display in this panel.

<a id="nestedblock--panel--text_panel"></a>

### Nested Schema for `panel.text_panel`

Required:

- `content` (String) The content of the text panel. Supports Markdown.

Each `panel` block must have exactly one of `query_panel`, `slo_panel`, or `text_panel` configured.

<a id="nestedblock--preset_filter"></a>

### Nested Schema for `preset_filter`

Required:

- `alias` (String) The alias for the preset filter.
- `column` (String) The column name for the preset filter.

For `chart` blocks in `visualization_settings`, the `chart_type` attribute accepts the following values: `line`, `tsbar`, `stacked`, `stat`, `cpie`, `cbar`, or `default`.

## Import

Boards can be imported using their ID, e.g.

```shell
terraform import honeycombio_flexible_board.my_board AobW9oAZX71
```

You can find the ID in the URL bar when visiting the board from the UI.

# Resource: honeycombio_slo

Creates a service level objective (SLO). For more information about SLOs, check out [Set Service Level Objectives (SLOs)](https://docs.honeycomb.io/working-with-your-data/slos/).

## Example Usage

### Single Dataset SLO

```terraform
resource "honeycombio_derived_column" "request_latency_sli" {
  alias       = "sli.request_latency"
  description = "SLI: request latency less than 300ms"
  dataset     = var.dataset

  # heredoc also works
  expression = file("../sli/sli.request_latency.honeycomb")

  lifecycle {
    create_before_destroy = true
  }
}

resource "honeycombio_slo" "slo" {
  name              = "Latency SLO"
  description       = "example of an SLO"
  dataset           = var.dataset
  sli               = honeycombio_derived_column.request_latency_sli.alias
  target_percentage = 99.9
  time_period       = 30

  tags = {
    team    = "blue"
    project = "secret"
  }
}
```

### Multi-Dataset SLO

```terraform
resource "honeycombio_derived_column" "request_latency_sli" {
  alias       = "sli.request_latency"
  description = "SLI: request latency less than 300ms"

  # heredoc also works
  expression = file("../sli/sli.request_latency.honeycomb")

  lifecycle {
    create_before_destroy = true
  }
}

resource "honeycombio_slo" "slo" {
  name              = "Latency SLO"
  description       = "example of an SLO"
  datasets          = [var.dataset1, var.dataset2]
  sli               = honeycombio_derived_column.request_latency_sli.alias
  target_percentage = 99.9
  time_period       = 30

  tags = {
    team         = "red"
    experimental = "true"
  }
}
```

-> **Note** As [Derived Columns](derived_column.md) cannot be renamed or deleted while in use, it is recommended to use the [create_before_destroy](https://developer.hashicorp.com/terraform/language/meta-arguments/lifecycle#create_before_destroy) lifecycle argument on your SLI resources as shown in the example above.
This way you will avoid running into conflicts if the Derived Column needs to be recreated.

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `name` (String) The name of the SLO.
- `sli` (String) The alias of the Derived Column that will be used as the SLI to indicate event success.
The derived column used as the SLI must be in the same dataset as the SLO. Additionally,
the column evaluation should consistently return nil, true, or false, as these are the only valid values for an SLI.
- `target_percentage` (Number) The percentage of qualified events that you expect to succeed during the `time_period`.
- `time_period` (Number) The time period, in days, over which your SLO will be evaluated.

### Optional

- `dataset` (String) The dataset this SLO is created in. Will be deprecated in a future release. Must be the same dataset as the SLI unless the SLI Derived Column is Environment-wide.
- `datasets` (Set of String) The datasets the SLO is evaluated on.
- `description` (String) A description of the SLO's intent and context.
- `tags` (Map of String) A map of tags to assign to the resource.

### Read-Only

- `id` (String) The ID of the SLO.

~> **Note** `dataset` will be deprecated in a future release. One of `dataset` or `datasets` is required. In the meantime, you can swap `dataset` with a single value array for `datasets` to effectively evaluate to the same configuration.

## Import

Single-dataset SLOs can be imported using by using their ID combined with their dataset.
If the SLO is a multi-dataset (MD) SLO, the dataset is not provided.

### SLO

```
$ terraform import honeycombio_slo.my_slo my-dataset/bj9BwOb1uKz
```

### Multi-dataset SLO

```
$ terraform import honeycombio_slo.my_slo bj9BwOb1uKz
```

You can find the ID in the URL bar when visiting the SLO from the UI.
